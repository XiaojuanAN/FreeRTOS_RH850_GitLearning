set(CMAKE_SYSTEM_NAME Generic)
set(CMAKE_TRY_COMPILE_TARGET_TYPE STATIC_LIBRARY)
set(CMAKE_C_COMPILER_WORKS TRUE) # make CMake happy with ccrh

set(CCRH_DIR "C:/Program Files (x86)/Renesas Electronics/CS+/CC/CC-RH/V2.03.00")

set(CMAKE_C_COMPILER    ${CCRH_DIR}/bin/ccrh.exe)
set(CMAKE_ASM_COMPILER  ${CCRH_DIR}/bin/asrh.exe)
set(CMAKE_CCRH_LINKER   ${CCRH_DIR}/bin/rlink.exe)

set(CMAKE_C_COMPILE_OBJECT      "<CMAKE_C_COMPILER> -c <SOURCE> <DEFINES> <INCLUDES> <FLAGS> -o<OBJECT>")
set(CMAKE_ASM_COMPILE_OBJECT    "<CMAKE_ASM_COMPILER> <SOURCE> <DEFINES> <INCLUDES> <FLAGS> -o<OBJECT>")
set(CMAKE_C_LINK_EXECUTABLE     "\"${CMAKE_CCRH_LINKER}\" <OBJECTS> <LINK_FLAGS> <LINK_LIBRARIES> -output=<TARGET>")

# How to get CC-RH to delete dead code?
# CC-RH's link-time optimization is applied when compiling with `-goptimize` and linking with `-optimize` and `-entry`.
#
# CC-RH seems to have a link-time optimization BUG.
# Only when compiling with `-Odefault` or `-Onothing`, CC-RH can delete all dead code.
#
# The following table shows the code size generated by compiling the source code of this repository (commit id 1911d95)
# with different -O options for CC-RH V2.03.00.
#
# | -O option | firmware.bin |
# | --------- | ------------ |
# | -Odefault | 17344        |
# | -Ospeed   | 19124        |
# | -Osize    | 19252        |
# | -Onothing | 24504        |
#
# The start execution address specified by `-entry=_start` should be as far ahead as possible,
# otherwise CC-RH's link-time optimization won't work at all.
set(CMAKE_C_FLAGS "-Xcpu=g3k -lang=c99 -Odefault -goptimize -g -g_line")
set(CMAKE_ASM_FLAGS "-Xcpu=g3k -goptimize -g")

set(LD_SCRIPT ${CMAKE_CURRENT_SOURCE_DIR}/sample/CCRH/rh850_ccrh.ld)

set(CMAKE_EXE_LINKER_FLAGS "-library=\"${CCRH_DIR}/lib/v850e3v5/libc\" \
                            -sub=${LD_SCRIPT} \
                            -list=${PROJECT_NAME}.map \
                            -show=all \
                            -optimize \
                            -entry=_start")

set(FREERTOS_TOOLCHAIN "CCRH")
